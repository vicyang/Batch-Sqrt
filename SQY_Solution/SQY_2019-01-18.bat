@echo off & SetLocal EnableDelayedExpansion
::变量a的值是被开方数，不得超过10000！
set a=29
::变量c的值是精确位数，不得超过2147483645！
set c=300
::【阶段1】计算整数部分
for %%a in (64 32 16 8 4 2 1) do set /a "b=(a0*%%a<<1)+%%a*%%a+b0" & if !a! geq !b! set /a "a0+=%%a","b0=b"
if !a! equ !b0! ( echo !a0! & exit /b ) else set /p "=!a0!."<nul

::【阶段2】计算小数点后几位，直到 结果的平方 与 被开方数 相差不到1。这点很重要。
for %%a in (512 256 128 64 32 16 8 4 2 1) do set /a "a1+=%%a","b=((a0*a1<<1)+a1*a1/1000)/1000+b0" & if !b! geq !a! set /a "a1-=%%a"
set "a=000!a1!" & set /p "=!a:~-3!"<nul & set /a "a=(a0*!a1!000<<1)+a1*a1","b0+=a/1000000","b1=(a/1000)%%1000","b2=a%%1000"

::【阶段3】计算小数点后剩余部分。
set /a "c=(c+2)/3+3" & for /l %%a in (1 1 !c!) do (
    
    :: 二分法模型，三位三位计算
    set "a=0" & for %%b in (512 256 128 64 32 16 8 4 2 1) do (
        :: 变量a为当前计算结果，如果得数超过三位，直接跳过计算并将得数减去本次增加的值。
        set /a "a+=%%b" & if 1000 gtr !a! (
            :: 变量c是余数
            set /a "b=(%%a<<1)+1","c=a*a/1000" & for /l %%c in (%%a -1 0) do set /a "c=((a%%c*a<<1)+c+b!b!)/1000","b-=1"
            :: 比较是否有溢出的值，如有，将得数减去本次增加的值。【已省略部分计算，所以在阶段3已经不需要和被开方数进行比较】
            set /a "c+=b!b!" & if !c! geq 1000 set /a "a-=%%b"
        ) else set /a "a-=%%b"
    )
    set "b=00!a!" & set /p "=!b:~-3!"<nul
    ::计算总结果的平方。利用完全平方公式 (1000a+b)^2 = 1000000a^2 + b^2 + 2000ab ，直接在原结果上增加。
    set /a "b=%%a+1" & set /a "a!b!=a","b<<=1" & set /a "b!b!=(a*a)%%1000","c=a*a/1000","b-=1"
    for /l %%b in (%%a -1 0) do set /a "b-=1","b!b!+=(a%%b*a<<1)+c","c=b!b!/1000","b!b!%%=1000"
)
echo. & exit /b